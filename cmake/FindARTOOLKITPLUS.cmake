# - Try to find ARToolKitPlus-library
# Once done this will define
#
#  ARTOOLKITPLUS_INCLUDE_DIR    - where to find ARToolKitPlus.h, etc.
#  ARTOOLKITPLUS_INCLUDE_DIRS   - same as above (uncached version)
#  ARTOOLKITPLUS_LIBRARIES      - list of libraries when using ARToolKitPlus.
#  ARTOOLKITPLUS_DEFINITIONS    - use ADD_DEFINITIONS(${ARTOOLKITPLUS_DEFINITIONS})
#  ARTOOLKITPLUS_FOUND          - True if ARToolKitPlus was found.

IF(ARTOOLKITPLUS_INCLUDE_DIR)
  SET(ARTOOLKITPLUS_FIND_QUIETLY TRUE)
ENDIF(ARTOOLKITPLUS_INCLUDE_DIR)

FIND_PATH(ARTOOLKITPLUS_INCLUDE_DIR ARToolKitPlus/ar.h
   PATHS
   $ENV{ARTOOLKITPLUS_HOME}/include
   $ENV{EXTERNLIBS}/ARToolKitPlus/include
   /usr/local/include
   /usr/include
   DOC "ARToolKitPlus - Headers"
   NO_DEFAULT_PATH
)
MARK_AS_ADVANCED(ARTOOLKITPLUS_INCLUDE_DIR)

IF (MSVC)
  
      FIND_LIBRARY(ARTOOLKITPLUS_LIBRARY_DEBUG NAMES ARToolKitPlusd ARToolKitPlusDll libARToolKitPlusd
        PATHS
        $ENV{ARTOOLKITPLUS_HOME}/lib
        $ENV{EXTERNLIBS}/ARToolKitPlus/lib
      )
      FIND_LIBRARY(ARTOOLKITPLUS_LIBRARY_RELEASE NAMES ARToolKitPlus ARToolKitPlusDll libARToolKitPlus
        PATHS
        $ENV{ARTOOLKITPLUS_HOME}/lib
        $ENV{EXTERNLIBS}/ARToolKitPlus/lib
      )

    IF(MSVC_IDE)
      IF (ARTOOLKITPLUS_LIBRARY_DEBUG AND ARTOOLKITPLUS_LIBRARY_RELEASE)
         SET(ARTOOLKITPLUS_LIBRARIES optimized ${ARTOOLKITPLUS_LIBRARY_RELEASE} debug ${ARTOOLKITPLUS_LIBRARY_DEBUG})
      ELSE (ARTOOLKITPLUS_LIBRARY_DEBUG AND ARTOOLKITPLUS_LIBRARY_RELEASE)
         SET(ARTOOLKITPLUS_LIBRARIES NOTFOUND)
         MESSAGE(STATUS "Could not find the debug AND release version of ARToolKitPlus")
      ENDIF (ARTOOLKITPLUS_LIBRARY_DEBUG AND ARTOOLKITPLUS_LIBRARY_RELEASE)
    ELSE(MSVC_IDE)
      STRING(TOLOWER ${CMAKE_BUILD_TYPE} CMAKE_BUILD_TYPE_TOLOWER)
      IF(CMAKE_BUILD_TYPE_TOLOWER MATCHES debug)
         SET(ARTOOLKITPLUS_LIBRARIES ${ARTOOLKITPLUS_LIBRARY_DEBUG})
      ELSE(CMAKE_BUILD_TYPE_TOLOWER MATCHES debug)
         SET(ARTOOLKITPLUS_LIBRARIES ${ARTOOLKITPLUS_LIBRARY_RELEASE})
      ENDIF(CMAKE_BUILD_TYPE_TOLOWER MATCHES debug)
    ENDIF(MSVC_IDE)
    MARK_AS_ADVANCED(ARTOOLKITPLUS_LIBRARY_DEBUG ARTOOLKITPLUS_LIBRARY_RELEASE)

ELSE (MSVC)
  
  SET(ARTOOLKITPLUS_NAMES ${ARTOOLKITPLUS_NAMES} ARToolKitPlus libARToolKitPlus)
  FIND_LIBRARY(ARTOOLKITPLUS_LIBRARY NAMES ${ARTOOLKITPLUS_NAMES}
    PATHS
    $ENV{ARTOOLKITPLUS_HOME}/lib
    $ENV{EXTERNLIBS}/ARToolKitPlus/lib
    NO_DEFAULT_PATH
  )
  FIND_LIBRARY(ARTOOLKITPLUS_LIBRARY NAMES ${ARTOOLKITPLUS_NAMES})

  IF (ARTOOLKITPLUS_LIBRARY)
    SET(ARTOOLKITPLUS_LIBRARIES ${ARTOOLKITPLUS_LIBRARY})
  ELSE (ARTOOLKITPLUS_LIBRARY)
    SET(ARTOOLKITPLUS_LIBRARIES NOTFOUND)
    MESSAGE(STATUS "Could not find ARToolKitPlus-library")    
  ENDIF (ARTOOLKITPLUS_LIBRARY)
  MARK_AS_ADVANCED(ARTOOLKITPLUS_LIBRARY)
  
ENDIF (MSVC)

INCLUDE(FindPackageHandleStandardArgs)

IF(MSVC)
  FIND_PACKAGE_HANDLE_STANDARD_ARGS(ARTOOLKITPLUS DEFAULT_MSG ARTOOLKITPLUS_LIBRARY_RELEASE ARTOOLKITPLUS_LIBRARY_DEBUG ARTOOLKITPLUS_INCLUDE_DIR)
ELSE(MSVC)
  FIND_PACKAGE_HANDLE_STANDARD_ARGS(ARTOOLKITPLUS DEFAULT_MSG ARTOOLKITPLUS_LIBRARY ARTOOLKITPLUS_INCLUDE_DIR)
ENDIF(MSVC)
