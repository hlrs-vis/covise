# @file
# 
# CMakeLists.txt for sys GuiRenderMessage (_coGRMsg)
#
# @author Blasius Czink

COVISE_FIND_PACKAGE(SWIG)
IF(NOT SWIG_FOUND)
  MESSAGE("SWIG Executable not found! Skipping GuiRenderMessage (_coGRMsg)")
  RETURN()
ENDIF()
INCLUDE(${SWIG_USE_FILE})

IF(NOT PYTHON_FOUND)
  #MESSAGE("PythonLibs not found! Skipping GuiRenderMessage (_coGRMsg)")
  RETURN()
ENDIF()

IF(WIN32)
  REMOVE_DEFINITIONS(-DDEBUG)
ENDIF(WIN32)

INCLUDE_DIRECTORIES(
  "${COVISEDIR}/src/kernel/grmsg"
)

SET(CMAKE_SWIG_FLAGS "")
#SET(CMAKE_SWIG_OUTDIR "${CMAKE_CURRENT_BINARY_DIR}")

SET(SWIGSOURCE "${CMAKE_CURRENT_SOURCE_DIR}/coGRMsg.i")
#SET(SWIGOUTPUT "${CMAKE_CURRENT_BINARY_DIR}/coGRMsg_wrap.cxx")

# SET_SOURCE_FILES_PROPERTIES(${SWIGOUTPUT} PROPERTIES GENERATED TRUE)
SET_SOURCE_FILES_PROPERTIES(${SWIGSOURCE} PROPERTIES CPLUSPLUS ON)
SET_SOURCE_FILES_PROPERTIES(${SWIGSOURCE} PROPERTIES SWIG_FLAGS "-DGRMSGEXPORT=")

if (${CMAKE_VERSION} VERSION_LESS "3.13")
  SWIG_ADD_MODULE(coGRMsg python ${SWIGSOURCE})
else()
  SWIG_ADD_LIBRARY(coGRMsg LANGUAGE python TYPE MODULE SOURCES ${SWIGSOURCE})
endif()
# ${SWIG_MODULE_coGRMsg_REAL_NAME} contains actual targetname
COVISE_ADJUST_OUTPUT_DIR(${SWIG_MODULE_coGRMsg_REAL_NAME})
SWIG_LINK_LIBRARIES(coGRMsg coGRMsg Python::Python)

# for VisualStudio it seems we have to rename debug versions to *_d.pyd
IF(MSVC)
    IF("${COVISE_ARCHSUFFIX}" STREQUAL "${BASEARCHSUFFIX}")
    SET_TARGET_PROPERTIES(${SWIG_MODULE_coGRMsg_REAL_NAME} PROPERTIES OUTPUT_NAME_DEBUG "_coGRMsg_d")
  ENDIF()
ENDIF(MSVC)

# place coGRMsg.py in the correct directory
IF(MSVC)
   ADD_CUSTOM_COMMAND(TARGET ${SWIG_MODULE_coGRMsg_REAL_NAME} POST_BUILD
      COMMAND ${CMAKE_COMMAND} -E copy_if_different ${CMAKE_CURRENT_BINARY_DIR}/coGRMsg.py $<TARGET_FILE_DIR:${SWIG_MODULE_coGRMsg_REAL_NAME}>
      )
ELSE(MSVC)
   ADD_CUSTOM_COMMAND(TARGET ${SWIG_MODULE_coGRMsg_REAL_NAME} POST_BUILD
      COMMAND ${CMAKE_COMMAND} -E copy_if_different ${CMAKE_CURRENT_BINARY_DIR}/coGRMsg.py '$<TARGET_FILE_DIR:${SWIG_MODULE_coGRMsg_REAL_NAME}>'
      )
ENDIF(MSVC)
